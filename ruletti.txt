   90 GOSUB 4500:REM ALKUPARAMETRIT
  100 GOSUB 4000:REM KUVA
  120 :
  130 GOSUB 1000:REM VALINNAT
  140 GOSUB 2000:REM PYORITYS
  150 GOSUB 3000:REM VOITOT
  160 IF RA>0 THEN 130
  170 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}RAHAT LOPPU..."
  180 PRINT"{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}'F1' - UUSI PELI"
  190 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}'F3' - LOPETUS"
  200 GET A$:IF A$=""THEN 200
  210 IF A$="{F1}"THEN RUN
  220 IF A$<>"{F3}"THEN 200
  230 PRINT"{CLR}":POKE 53280,14:POKE 53281,6
  240 END
  250  
 1000 REM ***** VALINNAT *****
 1010 POKE 650,128:PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}ASETA PANOKSESI."
 1015 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}'F1' = VALMIS"
 1020 PA=0:SN=0:FOR T=0 TO 26:SI(T)=0:NEXT T
 1025 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}                "
 1030 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}RAHAA :";RA;"MK."
 1035 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}                     "
 1040 PRINT"{UP}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}SIJOITETTU :";PA;"MK."
 1045 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}----------------------"
 1047 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}                     "
 1050 PRINT"{UP}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}'";
 1055 IF SN<23 THEN PRINT SN;:GOTO 1100
 1060 IF SN=23 THEN PRINT"{RVON}{GRY1} HARMAA {RVOF}{GRN}";
 1070 IF SN=24 THEN PRINT"{RVON}{RED} PUNAINEN {RVOF}{GRN}";
 1075 IF SN=25 THEN PRINT"1-11";
 1080 IF SN=26 THEN PRINT"12-22";
 1100 PRINT"' -";SI(SN);"MK."
 1120 GET A$:IF A$=""THEN 1120
 1130 IF A$<>"{F1}"THEN 1150
 1140 IF PA>0 THEN 1200
 1145 GOTO 1120
 1150 IF A$="@"AND SN>0 THEN SN=SN-1:GOTO 1025
 1160 IF A$="/"AND SN<26 THEN SN=SN+1:GOTO 1025
 1170 IF A$="+"AND RA>0 THEN SI(SN)=SI(SN)+1:RA=RA-1:PA=PA+1:GOTO 1025
 1180 IF A$="-"AND SI(SN)>0 THEN RA=RA+1:SI(SN)=SI(SN)-1:PA=PA-1:GOTO 1025
 1190 GOTO 1120
 1200 GOSUB 4700:POKE 650,0
 1240 FOR T=0 TO 22
 1250 IF SI(N(T))>0 THEN POKE 1024+X(T)+Y(T)*40,170
 1260 NEXT T
 1265 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}ODOTA..."
 1270 RETURN
 1990  
 2000 REM ***** PYORITYS *****
 2015 R=INT(RND(0)*23)+40
 2030 T=0:K=0:VI=1
 2050 POKE 781,Y(T):POKE 780,0:POKE 782,0:SYS 65520
 2055 IF INT(T/2)=T/2 THEN V1=2:V2=10:GOTO 2060
 2057 V1=11:V2=12
 2060 IF T=0 THEN V1=5:V2=13
 2065 IF K>R THEN VI=VI*1.5
 2070 POKE 646,V2
 2080 PRINT TAB(X(T))"{RGHT}{RVON}  {CBM-M}{RVOF}"
 2085 PRINT TAB(X(T))"{RVON} {RGHT}{RGHT}{CBM-M}{RVOF}"
 2090 PRINT TAB(X(T))"{RVON}{CBM-P}{CBM-P}{CBM-P}{SHIFT-@}{RVOF}{UP}{UP}{UP}"
 2095 POKE 646,V1:FOR TT=1 TO VI:NEXT TT
 2097 IF VI>800 THEN 2160
 2100 PRINT TAB(X(T))"{RGHT}{RVON}  {CBM-M}{RVOF}"
 2105 PRINT TAB(X(T))"{RVON} {RGHT}{RGHT}{CBM-M}{RVOF}"
 2110 PRINT TAB(X(T))"{RVON}{CBM-P}{CBM-P}{CBM-P}{SHIFT-@}{RVOF}"
 2120 T=T+1:IF T=23 THEN T=0
 2130 K=K+1
 2150 GOTO 2050
 2160 T=N(T)
 2170 VV=T
 2190 RETURN
 2990 :
 3000 REM ***** VOITOT *****
 3030 GOSUB 4700
 3040 PRINT"{GRN}{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}VOITOT:"
 3050 VO=0:PRINT:FOR T=0 TO 22
 3060 IF T<>VV THEN 3070
 3062 IF SI(T)=0 THEN 3070
 3065 VO=VO+SI(T)*22:PRINT"{GRN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}NO.";VV;" VOIT."SI(T)*22"MK."
 3070 NEXT T
 3075 IF VV>12 OR SI(25)<1 THEN 3082
 3080 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}'1-11' VOIT. "SI(25)*2"MK.":VO=VO+SI(25)*2:GOTO 3090
 3082 IF SI(26)<1 THEN 3090
 3085 IF VV>11 THEN PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}'12-22' VOIT."SI(26)*2"MK.":VO=VO+SI(26)*2
 3090 IF VV=0 THEN 3120
 3092 IF INT(VV/2)<>VV/2 THEN 3100
 3093 IF SI(23)=0 THEN 3120
 3095 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RVON}{GRY1}HARMAA{RVOF}{GRN} VOIT.";SI(23)*2"MK.":VO=VO+SI(23)*2
 3097 GOTO 3120
 3100 IF SI(24)=0 THEN 3120
 3110 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RVON}{RED}PUNAINEN{RVOF}{GRN} VOIT.";SI(24)*2"MK.":VO=VO+SI(24)*2
 3120 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}VOITOT YHT. :";VO;"MK."
 3130 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}SIJOITUKSET :";PA
 3150 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}TULOS       :";VO-PA"MK."
 3160 RA=RA+VO
 3170 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}RAHAA       :";RA
 3180 PRINT"{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}    PAINA 'F1'"
 3185 POKE 198,0
 3190 GET A$:IF A$<>"{F1}"THEN 3190
 3195 R=1:GOSUB 4085:GOSUB 4700:PRINT"{GRN}"
 3200 RETURN
 3990 :
 4000 REM **** KUVA ****
 4010 PRINT"{CLR}":POKE 53281,0:POKE 53280,0
 4020 PRINT"{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{ORNG}"
 4030 PRINT"        {CBM-C}{RVON}{CBM-D}{CBM-F}{RVOF}{CBM-F}{RVON}{CBM-F}{RVOF}{CBM-V}{RVON}{CBM-F}{RVOF}{CBM-V}{RVON}{CBM-F}{RVOF}{CBM-V} {RVON}{CBM-F}{CBM-I}{RVOF}{CBM-K}{RVON}{CBM-D}{CBM-D}{RVOF}{CBM-K}{RVON}{CBM-D}{CBM-D}{RVOF}{CBM-K}{RVON}{CBM-F}{RVOF}{CBM-V}"
 4040 PRINT"         {CBM-K}{CBM-D}{CBM-K}{RVON}{CBM-K}{RVOF} {RVON}{CBM-K}{RVOF} {RVON}{CBM-K}{RVOF}  {RVON}{CBM-K}{RVOF}{CBM-D}  {CBM-K}  {CBM-K}{RVOF} {RVON}{CBM-K}{RVOF}{BRN}"
 4050 PRINT"        {CBM-C}{RVON}{CBM-D}{CBM-D}{RVOF} {RVON}{CBM-F}{RVOF}{CBM-V}{RVON}{CBM-F}{RVOF}{CBM-V}{RVON}{CBM-F}{RVOF}{CBM-V} {RVON}{CBM-F}{CBM-F}{RVOF} {CBM-C}{RVON}{CBM-D}{RVOF} {CBM-C}{RVON}{CBM-D}{RVOF} {RVON}{CBM-F}{RVOF}{CBM-V}"
 4060 PRINT" {RGHT}       {CBM-K}{RVON}{CBM-F}{RVOF} {RVON}{CBM-K}{RVOF}{CBM-F}{RVON}{CBM-V}{RVOF} {RVON}{CBM-K}{RVOF} {CBM-F}{RVON}{CBM-K}{RVOF} {CBM-F} {CBM-K}  {CBM-K} {RVON}{CBM-K}{RVOF}"
 4070 PRINT"        {CBM-C}{RVON}{CBM-I}{RVOF}{CBM-C}{CBM-V} {RVON}{CBM-I}{RVOF}{CBM-V} {RVON}{CBM-I}{CBM-I}{RVOF}{CBM-V}{RVON}{CBM-I}{CBM-I}{RVOF}{CBM-V}{CBM-C}{RVON}{CBM-I}{RVOF} {CBM-C}{RVON}{CBM-I}{RVOF} {RVON}{CBM-I}{RVOF}{CBM-V}"
 4080 FOR T=1 TO 19:PRINT:FOR TT=1 TO 20:NEXT TT:NEXT T
 4082 GOSUB 4700
 4085 FOR T=0 TO 22:POKE 781,Y(T):POKE 780,0:POKE 782,0:SYS 65520
 4090 V=11:IF T=0 THEN V=5:GOTO 4110
 4100 IF INT(T/2)=T/2 THEN V=2:GOTO 4110
 4110 POKE 646,V:A$=STR$(N(T)):A$="{RVON}"+A$
 4120 PRINT TAB(X(T))"{RVON}   {CBM-M}{RVOF}"
 4130 PRINT TAB(X(T))"{RVON}   {CBM-M}{RVOF}"
 4140 PRINT TAB(X(T))"{RVON}{CBM-P}{CBM-P}{CBM-P}{SHIFT-@}{RVOF}"
 4150 PRINT"{UP}{UP}";TAB(X(T))A$
 4170 NEXT T
 4195 IF R=1 THEN R=0:RETURN
 4200 M=1:POKE 198,0
 4210 M$=MID$(TX$,M,22)
 4220 PRINT"{GRN}{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}";M$
 4225 FOR T=1 TO 50:NEXT T
 4230 GET A$:IF A$="{F1}"THEN 4300
 4240 M=M+1:IF M=115 THEN RUN
 4250 GOTO 4210
 4300 :
 4400 GOSUB 4700:POKE 650,128
 4410 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}PALJONKO RAHAA ?"
 4415 PRINT"{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}'F1' = VALMIS"
 4420 RA=10
 4425 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}                     "
 4430 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}RAHAA :";RA;"MK."
 4440 GET A$:IF A$=""THEN 4440
 4450 IF A$="{F1}"THEN GOSUB 4700:RETURN
 4460 IF A$="+"THEN RA=RA+1
 4470 IF A$="-"THEN RA=RA-1
 4475 IF RA=0 THEN RA=1
 4477 IF RA=101 THEN RA=100
 4480 GOTO 4425
 4500  
 4510 REM **** PARAMETRIT ****
 4520 TX$="                     RULETIN ON TEHNYT ROBERT BROTHERUS 17.4 - 1987."
 4530 TX$=TX$+"..........PAINA 'F1' ALOITTAAKSESI PELIN.                       "
 4540 DIM N(23),X(23),Y(23),SI(27)
 4550 FOR T=0 TO 22:READ N(T),X(T),Y(T):NEXT T
 4560 DATA 0,17,5,8,23,5,1,27,5,16,31,5
 4565 DATA 5,31,8,10,31,11,7,31,14
 4570 DATA 20,31,17,3,31,20,12,27,20
 4575 DATA 17,23,20,14,19,20,19,15,20
 4580 DATA 6,11,20,9,7,20,2,3,20
 4585 DATA 21,3,17,22,3,14,11,3,11
 4590 DATA 18,3,8,15,3,5,4,7,5,13,11,5
 4640 RETURN
 4650  
 4700 REM ****** TYHJENNYS *****
 4710 PRINT"{GRY3}{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}";
 4720 PRINT TAB(7)"UCCCCCCCCCCCCCCCCCCCCCCI"
 4730 PRINT TAB(7)"B                      B"
 4740 PRINT TAB(7)"{CBM-Q}CCCCCCCCCCCCCCCCCCCCCC{CBM-W}"
 4750 FOR T=1 TO 8:PRINT TAB(7)"B                      B":NEXT T
 4760 PRINT TAB(7)"JCCCCCCCCCCCCCCCCCCCCCCK{GRN}"
 4770 RETURN
